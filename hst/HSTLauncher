; HST Launcher
; ------------
;
; Date: 2018-05-08
; Author: Henrik Noerfjand Stengaard
;
; An AmigaDOS script to show a menu for launching games or demos.


; Clear screen
echo "*ec" NOLINE

; Error, if HSTLauncher: assign doesn't exist
Assign >NIL: EXISTS HSTLauncher:
IF WARN
  REQUESTCHOICE "Error" "HST Launcher assign HSTLauncher: doesn't exist!" "OK" >NIL:
  SKIP end
ENDIF

; Error, if menu file doesn't exist
IF NOT EXISTS "HSTLauncher:menu.lst"
  REQUESTCHOICE "Error" "HST Launcher menu file 'HSTLauncher:menu.lst'*Ndoesn't exist!" "OK" >NIL:
  SKIP end
ENDIF

; Start music
IF EXISTS "ENV:hstlaunchermusic"
  IF EXISTS "S:HSTLauncherMusic"
    echo "`execute S:HSTLauncherMusic START`"
  ENDIF
ENDIF

; reset hst menu dirs
echo "" >"T:_hstmenudirs"


; hst menu
; --------
LAB hstmenu

; get hstmenudir
set hstmenudirindex `wc -l <"T:_hstmenudirs"`
set hstmenudir ""
set hstmenudir `sed "1q;d" "T:_hstmenudirs"`

; build hst menu
echo "Find" >"T:_hstmenu"
echo "Settings" >>"T:_hstmenu"

IF $hstmenudirindex GT 1 VAL
  echo "Back" >>"T:_hstmenu"
ELSE
  echo "Quit" >>"T:_hstmenu"
ENDIF

echo "----------------------------------------" >>"T:_hstmenu"

echo "HST Launcher" NOLINE >"T:_hstmenutitle"
IF "$hstmenudir" EQ ""
  cut -f 1 "HSTLauncher:menu.lst" >"T:_hstmenuitemnames"
  
  IF EXISTS "HSTLauncher:title.txt"
    echo ", " NOLINE >>"T:_hstmenutitle"
    type "HSTLauncher:title.txt" >>"T:_hstmenutitle"
  ENDIF
ELSE
  cut -f 1 "HSTLauncher:$hstmenudir/menu.lst" >"T:_hstmenuitemnames"

  echo ", " NOLINE >>"T:_hstmenutitle"
  IF EXISTS "HSTLauncher:$hstmenudir/title.txt"
    type "HSTLauncher:$hstmenudir/title.txt" >>"T:_hstmenutitle"
  ELSE
    echo "$hstmenudir" >>"T:_hstmenutitle"
  ENDIF
ENDIF
set hstmenutitle "`type "T:_hstmenutitle"`"
sed "1d" "T:_hstmenuitemnames" >>"T:_hstmenu"

; show hst menu
set hstmenuindex ""
set hstmenuindex "`RequestList TITLE="$hstmenutitle" LISTFILE="T:_hstmenu" WIDTH=640 LINES=24`"
Delete "T:_hstmenu" >NIL:

; end, if request list was closed
IF NOT "$hstmenuindex" GT 0 VAL
  SKIP end
ENDIF

; find option
IF "$hstmenuindex" EQ 1 VAL
  SKIP find
ENDIF

; settings option
IF "$hstmenuindex" EQ 2 VAL
  echo ".Settings" >"T:_hstmenudirs"
  echo "" >>"T:_hstmenudirs"
  SKIP BACK hstmenu
ENDIF

; quit or back option
IF "$hstmenuindex" EQ 3 VAL
  IF $hstmenudirindex GT 1 VAL
    ; back
    sed "1d" "T:_hstmenudirs" >"T:_hstmenudirs_new"
    copy "T:_hstmenudirs_new" "T:_hstmenudirs" >NIL:
    delete "T:_hstmenudirs_new" >NIL:
    SKIP BACK hstmenu
  ELSE
    ; quit
    SKIP end
  ENDIF
ENDIF

; hst menu, if hstmenuindex is less than 5
IF NOT "$hstmenuindex" GT 4 VAL
  SKIP BACK hstmenu
ENDIF

; get hstmenuitempath
set hstmenuindex `eval $hstmenuindex - 4` 
echo "$hstmenuindex" >T:_hstmenuindex
set hstmenulineregexp `sed "s/\(.\)*$/\1q;d/" T:_hstmenuindex`
IF "$hstmenudir" EQ ""
  sed "$hstmenulineregexp" "HSTLauncher:menu.lst" >T:_hstmenuline
ELSE
  sed "$hstmenulineregexp" "HSTLauncher:$hstmenudir/menu.lst" >T:_hstmenuline
ENDIF
cut -f 2 "T:_hstmenuline" >T:_hstmenuitempath
set hstmenuitempath "`sed "1d" "T:_hstmenuitempath"`"

; add hstmenuitempath to hstmenudirs, if hstmenuitempath is a menu
IF EXISTS "HSTLauncher:$hstmenuitempath/menu.lst"
  echo "$hstmenuitempath" >"T:_hstmenudirs_new"
  type "T:_hstmenudirs" >>"T:_hstmenudirs_new"
  copy "T:_hstmenudirs_new" "T:_hstmenudirs" >NIL:
  delete "T:_hstmenudirs_new" >NIL:
  SKIP BACK hstmenu
ENDIF

; execute hstmenuitempath run file, if it exists
IF EXISTS "HSTLauncher:$hstmenuitempath.run"
  set returnlab "hstmenu"
  set runfile "HSTLauncher:$hstmenuitempath.run"
  SKIP run
ELSE
  REQUESTCHOICE "Error" "HST Launcher run file 'HSTLauncher:$hstmenuitempath.run'*Ndoesn't exist!" "OK" >NIL:
ENDIF

SKIP BACK hstmenu


; find
; ----
LAB find

; error, if search file doesn't exist
IF NOT EXISTS "HSTLauncher:search.lst"
  REQUESTCHOICE "Error" "HST Launcher search file 'HSTLauncher:search.lst'*Ndoesn't exist!" "OK" >NIL:
  SKIP BACK hstmenu
ENDIF

; enter search text
Delete >NIL: T:_result#?
set searchtext `RequestString "Find" "Enter search text"` 

; find search text in search file and limit to first 25 results
search >T:_result "HSTLauncher:search.lst" "$searchtext" NONUM
head >T:_resultlimited -25 T:_result
cut >T:_resultnames1 -f 1 T:_resultlimited

; build find results menu
echo "Find" >T:_resultnames2
echo "Back" >>T:_resultnames2
echo "----------------------------------------" >>T:_resultnames2
sed >>T:_resultnames2 "1d" T:_resultnames1
cut -f 2 T:_resultlimited >>T:_resultrunfiles1
echo "" >T:_resultrunfiles2
echo "" >>T:_resultrunfiles2
echo "" >>T:_resultrunfiles2
sed >>T:_resultrunfiles2 "1d" T:_resultrunfiles1


; result
; ------
LAB result

; show result menu
set resultindex ""
set resultindex "`RequestList TITLE="HST Launcher, Find '$searchtext' results" LISTFILE="T:_resultnames2" WIDTH=640 LINES=24`"

; hst menu, if index is less than 1
IF NOT "$resultindex" GT 0 VAL
  Delete >NIL: T:_result#?
  SKIP BACK hstmenu
ENDIF

; find, if index is 1
IF "$resultindex" EQ 1 VAL
  SKIP BACK find
ENDIF

; hst menu, if index is 2
IF "$resultindex" EQ 2 VAL
  Delete >NIL: T:_result#?
  SKIP BACK hstmenu
ENDIF

; result, if index is less than 4
IF NOT "$resultindex" GT 3 VAL
  SKIP BACK result
ENDIF

; get hstmenuitempath from result
echo "$resultindex" >T:_resultindex
set resultindexregexp `sed "s/\(.\)*$/\1q;d/" T:_resultindex`
sed "$resultindexregexp" "T:_resultrunfiles2" >T:_hstmenuline
cut -f 2 "T:_hstmenuline" >T:_hstmenuitempath
set hstmenuitempath "`sed "1d" "T:_hstmenuitempath"`"

; execute hstmenuitempath run file, if it exists
IF EXISTS "HSTLauncher:$hstmenuitempath.run"
  set returnlab "result"
  set runfile "HSTLauncher:$hstmenuitempath.run"
  SKIP run
ELSE
  REQUESTCHOICE "Error" "HST Launcher run file 'HSTLauncher:$hstmenuitempath.run'*Ndoesn't exist!" "OK" >NIL:
ENDIF

SKIP BACK result


; run
; ---
LAB run

; stop music
IF EXISTS "ENV:hstlaunchermusic"
  IF EXISTS "S:HSTLauncherMusic"
    echo "`execute S:HSTLauncherMusic STOP`"
  ENDIF
ENDIF

; start run file, if run file exists
echo "Starting '$runfile'..."
IF EXISTS "$runfile"
  echo "*ec" NOLINE
  avail >NIL: flush
  echo "`execute "$runfile"`"
  wait 1
  echo "*ec" NOLINE
ENDIF

; start music, if hstlaunchermusic exists
IF EXISTS "ENV:hstlaunchermusic"
  IF EXISTS "S:HSTLauncherMusic"
    echo "`execute S:HSTLauncherMusic START`"
  ENDIF
ENDIF

SKIP BACK $returnlab


; end
; ---
LAB end

; stop music, if hstlaunchermusic exists
IF EXISTS "S:HSTLauncherMusic"
  echo "`execute S:HSTLauncherMusic STOP`"
ENDIF
